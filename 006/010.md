Acrobatic Eggshell Snail

high

# `BridgeRelay` deposits assets on behalf of itself and not the caller.

## Summary

The [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) exposes functions which enable callers to bridge their assets to Polygon, however the bridged assets are redeemable only to the address of the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) itself on the Polygon chain, and not the `msg.sender` who initialized the migration and supplied the assets to bridge.

## Vulnerability Detail

The bridge relay exposes two execution paths which enable callers to deposit assets onto Polygon:

```solidity
/**
 * @notice calls Polygon POS bridge for deposit
 * @dev the contract is designed in a way where anyone can call the function without risking funds
 * @dev MATIC cannot be bridged
 * @param token address of the token that is desired to be pushed accross the bridge
 */
function bridgeTransfer(IERC20 token) external payable {
    // revert if MATIC is attempted
    if (token == MATIC) revert MATICUnbridgeable();
    // unwrap WETH
    if (token == WETH) {
        IERC20Withdrawable(address(WETH)).withdraw(
            WETH.balanceOf(address(this))
        );
    // transfer ERC20 tokens
    } else if (token != ETHER) {
        transferERCToBridge(token);
        return;
    }
    // transfer ETHER
@>  POS_BRIDGE.depositEtherFor{value: address(this).balance}(address(this));
}
```

```solidity
/**
 * @notice pushes token transfers through to the PoS bridge
 * @dev this is for ERC20 tokens that are not the matic token
 * @dev only tokens that are already mapped on the bridge will succeed
 * @param token is address of the token that is desired to be pushed accross the bridge
 */
function transferERCToBridge(IERC20 token) internal {
    //zero out approvals
    token.forceApprove(PREDICATE_ADDRESS, 0);
    // increase approval to necessary amount
    token.safeIncreaseAllowance(
        PREDICATE_ADDRESS,
        token.balanceOf(address(this))
    );
    //deposit
    POS_BRIDGE.depositFor(
@>      address(this),
        address(token),
        abi.encodePacked(token.balanceOf(address(this)))
    );
}
```

Notice that in both calls to [`depositFor`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L76C9-L80C11) and [`depositEtherFor`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L58C9-L58C81), the deposits are made to the account `address(this)`, which is the address of the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) itself.

Since we know that the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) is only going to be [deployed on Ethereum only](https://discord.com/channels/812037309376495636/1214225865546661920/1214266748828913734), bridged assets will be sent to an uninitialized contract address on Polygon, resulting in a loss for callers.

> [!NOTE]
> 
> The severity of this issue can be downgraded if the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) intends to be invoked via a proxy contract deployed to a deterministic address. In this case, it would be possible to rescue funds, since the sponsor could feasibly deploy a matching proxy to the target address on Polygon and provide a custom `implementation` capable of rescuing the funds.
>
> However, if the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) is intended to be immutable (even if deployed via `CREATE2`), the current implementation lacks sufficient controls to permit bridged assets to be rescued.

## Impact

High - bridged assets can be potentially be lost forever contingent upon the intended deployment architecture.

## Code Snippet

```solidity
/**
 * @notice calls Polygon POS bridge for deposit
 * @dev the contract is designed in a way where anyone can call the function without risking funds
 * @dev MATIC cannot be bridged
 * @param token address of the token that is desired to be pushed accross the bridge
 */
function bridgeTransfer(IERC20 token) external payable {
    // revert if MATIC is attempted
    if (token == MATIC) revert MATICUnbridgeable();
    // unwrap WETH
    if (token == WETH) {
        IERC20Withdrawable(address(WETH)).withdraw(
            WETH.balanceOf(address(this))
        );
    // transfer ERC20 tokens
    } else if (token != ETHER) {
        transferERCToBridge(token);
        return;
    }
    // transfer ETHER
    POS_BRIDGE.depositEtherFor{value: address(this).balance}(address(this));
}

/**
 * @notice pushes token transfers through to the PoS bridge
 * @dev this is for ERC20 tokens that are not the matic token
 * @dev only tokens that are already mapped on the bridge will succeed
 * @param token is address of the token that is desired to be pushed accross the bridge
 */
function transferERCToBridge(IERC20 token) internal {
    //zero out approvals
    token.forceApprove(PREDICATE_ADDRESS, 0);
    // increase approval to necessary amount
    token.safeIncreaseAllowance(
        PREDICATE_ADDRESS,
        token.balanceOf(address(this))
    );
    //deposit
    POS_BRIDGE.depositFor(
        address(this),
        address(token),
        abi.encodePacked(token.balanceOf(address(this)))
    );
}
```

## Tool used

Manual Review

## Recommendation

Deposit the assets on behalf of an intended `recipient` instead of `address(this)`.
