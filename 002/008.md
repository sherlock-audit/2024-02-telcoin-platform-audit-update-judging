Exotic Concrete Boa

high

# Slippage because of missing deadline parameter in `StablecoinSwap`

## Summary
[AmirX.stablecoinSwap()](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/swap/AmirX.sol#L66-L94) is used for swapping between two tokens. The swap uses  the `StablecoinSwap` struct as its configuration

```solidity
    struct StablecoinSwap {
        // recipient of the target currency
        address destination;
        // the originating currency
        address origin;
        // the amount of currency being provided
        uint256 oAmount;
        // the target currency
        address target;
        // the amount of currency to be provided
        uint256 tAmount;
    }
``` 
The swap is executed with fixed amounts and without a deadline parameter which increases risks of slippage.

## Vulnerability Detail
The problem may occur on different swap paths, for example:
 1. `stablecoin -> stablecoin` - the stablecoins will be regularly minted/burned to reflect the price of the offchain asset they are backed with.  The value of X stable coins may be quite different from what it was when the swap transaction was submitted. 
 2. `stablecoin -> ERC20 and vice versa` - the same explanation from above holds true for this case, but here the risk is even greater because a swap may be executed with a highly volatile ERC20 token. As per the docs, any ERC20s are expected to interact with the protocol.

The missing deadline parameter can also be leveraged by a malicious entity to perform a block stuffing attack in order to hold the swap transaction's execution for a later point in time when the MEV is at its highest. The contract will be deployed to Polygon so this scenario is very likely.

## Impact
 1. Loss of funds for the protocol and a really high profit for an attacker when trading with highly volatile tokens.
 1. Accounting errors because of unexpected amounts being minted/swapped.

## Code Snippet
```solidity
    function stablecoinSwap(
        address wallet,
        address safe,
        StablecoinSwap memory ss,
        DefiSwap memory defi
    ) external payable onlyRole(SWAPPER_ROLE) {
        // checks if it will fail
        _verifyStablecoin(wallet, safe, ss, defi);

        //eXYZ ot eXYZ
        if (isXYZ(ss.origin) && isXYZ(ss.target)) {
            swapAndSend(wallet, ss);
            return;
        }

        //stablecoin swap
        if (isXYZ(ss.origin) && !isXYZ(ss.target))
            convertFromEXYZ(wallet, safe, ss);

        //defi swap
        uint256 iBalance = ERC20(ss.origin).balanceOf(wallet);
        if (defi.walletData.length != 0) defiSwap(wallet, safe, defi);
        uint256 fBalance = ERC20(ss.origin).balanceOf(wallet);
        // //stablecoin swap
        if (!isXYZ(ss.origin) && isXYZ(ss.target)) {
            if (fBalance - iBalance != 0) ss.oAmount = fBalance - iBalance;
            convertToEXYZ(wallet, safe, ss);
        }
    }
```

```solidity
    function convertToEXYZ(
        address wallet,
        address safe,
        StablecoinSwap memory ss
    ) public virtual whenNotPaused nonZero(ss) onlyRole(SWAPPER_ROLE) {
        if (
            Stablecoin(ss.target).totalSupply() + ss.tAmount >
            getMaxLimit(ss.target)
        ) revert InvalidMintBurnBoundry(ss.target);

        ERC20PermitUpgradeable(ss.origin).safeTransferFrom(
            wallet,
            safe,
            ss.oAmount
        );
        Stablecoin(ss.target).mintTo(ss.destination, ss.tAmount);
    }
```

```solidity
    function convertFromEXYZ(
        address wallet,
        address safe,
        StablecoinSwap memory ss
    ) public virtual whenNotPaused nonZero(ss) onlyRole(SWAPPER_ROLE) {
        if (
            Stablecoin(ss.origin).totalSupply() - ss.oAmount <
            getMinLimit(ss.origin)
        ) revert InvalidMintBurnBoundry(ss.origin);

        Stablecoin(ss.origin).burnFrom(wallet, ss.oAmount);
        ERC20PermitUpgradeable(ss.target).safeTransferFrom(
            safe,
            ss.destination,
            ss.tAmount
        );
    }
```
## Tool used

Manual Review

## Recommendation
Add a deadline parameter to `StablecoinSwap` and do not execute expired swaps.

```diff
    struct StablecoinSwap {
        // recipient of the target currency
        address destination;
        // the originating currency
        address origin;
        // the amount of currency being provided
        uint256 oAmount;
        // the target currency
        address target;
        // the amount of currency to be provided
        uint256 tAmount;
+     // deadline parameter for swap expirations
+     uint256 deadline;
    }
``` 
