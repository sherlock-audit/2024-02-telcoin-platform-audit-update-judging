Acrobatic Eggshell Snail

medium

# External protocol changes to the `RootChainManager` can invalidate hardcoded `PREDICATE_ADDRESS`.

## Summary

The [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) makes a fixed assumption about the [`PREDICATE_ADDRESS`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L31C5-L33C52) to use when bridging ERC-20s, but the associated predicate for a given `tokenType` is subject to change.

## Vulnerability Detail

During a call to [`transferERCToBridge(address)`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L67C14-L67C47), the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) approves the [`PREDICATE_ADDRESS`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L31C5-L33C52) to spend the current token balance of the contract:

```solidity
/**
 * @notice pushes token transfers through to the PoS bridge
 * @dev this is for ERC20 tokens that are not the matic token
 * @dev only tokens that are already mapped on the bridge will succeed
 * @param token is address of the token that is desired to be pushed accross the bridge
 */
function transferERCToBridge(IERC20 token) internal {
    //zero out approvals
    token.forceApprove(PREDICATE_ADDRESS, 0);
    // increase approval to necessary amount
    token.safeIncreaseAllowance(
        PREDICATE_ADDRESS,
        token.balanceOf(address(this))
    );
    //deposit
    POS_BRIDGE.depositFor(
        address(this),
        address(token),
        abi.encodePacked(token.balanceOf(address(this)))
    );
}
```

The assumption here is that the execution flow of `depositFor` will invoke the associated predicate contract to `transferFrom` and lock the specified `token` amount. However, if we take a look at the [`RootChainManager`](https://etherscan.io/address/0x37d26dc2890b35924b40574bac10552794771997#code) we can that the selected predicate contract for a given `tokenType` is _not_ immutable:

```solidity
/**
 * @notice Register a token predicate address against its type, callable only by ADMIN
 * @dev A predicate is a contract responsible to process the token specific logic while locking or exiting tokens
 * @param tokenType bytes32 unique identifier for the token type
 * @param predicateAddress address of token predicate address
 */
function registerPredicate(bytes32 tokenType, address predicateAddress)
    external
    override
    only(DEFAULT_ADMIN_ROLE)
{
    typeToPredicate[tokenType] = predicateAddress;
    emit PredicateRegistered(tokenType, predicateAddress);
}
```

This means that in the event of a change in the resolved predicate address, tokens queued for bridging will be approved to spend for a contract which is no longer invoked, resulting in stuck tokens on the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) which cannot be rescued via the understandably-constrained [`erc20Rescue(address)`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L88C14-L88C25) function.

In such instances, the call to [`transferERCToBridge`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L67) will terminate successfully, leaving the assets stuck on the contract.

## Impact

Bridged assets can become stuck in the [`BridgeRelay`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/main/telcoin-contracts/contracts/bridge/BridgeRelay.sol) if the predicate address is changed in future due to a protocol improvement or hack.

## Code Snippet

```solidity
// mainnet PoS bridge
IPOSBridge public constant POS_BRIDGE =
    IPOSBridge(0xA0c68C638235ee32657e8f720a23ceC1bFc77C77);
```

## Tool used

Manual Review

## Recommendation

Do not use a hardcoded [`PREDICATE_ADDRESS`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L31C5-L33C52), and instead compute the appropriate address dynamically:

```diff
- // mainnet predicate
- address public constant PREDICATE_ADDRESS = 0x40ec5B33f54e0E8A33A975908C5BA1c14e5BbbDf;

+ error MissingPredicateFor(address token);

+ /**
+  * @notice Determines the correct predicate address to send ERC-20
+  * tokens to when bridging assets to the Polygon network.
+  * @param token The asset to bridge to Polygon.
+  * @return predicate The address of the predicate contract which
+  * receives the asset to be bridged.
+  */
+ function _getPredicateAddressOrRevert(IERC20 token) internal view returns (address predicate) {
+   
+   predicate = IRootChainManager(POS_BRIDGE).typeToPredicate(
+     IRootChainManager(POS_BRIDGE).tokenToType(address(token))
+   );
+     
+   if (predicate == address(0)) revert MissingPredicateFor(address(token));
+ }
```

However, we cannot rule out that using a fixed predicate address may be a desirable immutable property from the perspective of the sponsor, though this will not protect against external protocol changes.

In this scenario, we recommend that calls to [`transferERCToBridge`](https://github.com/sherlock-audit/2024-02-telcoin-platform-audit-update/blob/21920190e0772afa18e7f856a036fea3ef5b9635/telcoin-contracts/contracts/bridge/BridgeRelay.sol#L67) should `revert` if the full token allowance has not been spent:

```diff
+ error PredicateFailedToSpendTokens(address token, uint256 amount);

/**
 * @notice pushes token transfers through to the PoS bridge
 * @dev this is for ERC20 tokens that are not the matic token
 * @dev only tokens that are already mapped on the bridge will succeed
 * @param token is address of the token that is desired to be pushed accross the bridge
 */
function transferERCToBridge(IERC20 token) internal {
    //zero out approvals
    token.forceApprove(PREDICATE_ADDRESS, 0);
    // increase approval to necessary amount
    token.safeIncreaseAllowance(
        PREDICATE_ADDRESS,
        token.balanceOf(address(this))
    );
    //deposit
    POS_BRIDGE.depositFor(
        address(this),
        address(token),
        abi.encodePacked(token.balanceOf(address(this)))
    );
+
+   if (token.balanceOf(address(this) != 0)
+     revert PredicateFailedToSpendTokens(address(token), token.balanceOf(address(this)));
}
```

This will ensure the caller's tokens do not get stuck.